# Various utility functions.
#
# Please do not add any FST exports to this file.

import 'inventory.grm' as i;

# Performs an unconditioned rewrite.
func Rewrite[tau, sigma_star] {
    return CDRewrite[tau, "", "", sigma_star];
}

# Rewrites an entire word using the inventory definitions
# of BOW and EOW.
func RewriteWord[tau, sigma_star] {
    return CDRewrite[tau, i.BOW, i.EOW, sigma_star];
}

# Filters rule (cf. FSTP, p. 70) on closures of input and output vocabularies.
func FilterRule[rule, input, output] {
    return input* @ rule @ output*;
}

# Joins together the argument FST, separated by a particular delimiter.
func Join[arg, sep] {
  return arg (sep arg)*;
}

# Deletes the passed string.
func Delete[tau] {
    return tau : "";
}

# Inserts the passed string.
func Insert[tau] {
    return "" : tau;
}

func AssertInputEqual[input, rule, expected] {
     applied = input @ rule;
     return AssertEqual[Project[applied, 'input'], expected];
}