# Implements sandhi phenomena, such as elision, resyllabification,
# diaeresis, synizesis, hypermetric lengthening, etc.

import 'byte.grm' as b;
import 'inventory.grm' as i;
import 'utility.grm' as u;

sigma_star = (i.PROSODIC_SYMBOL | i.PHONEME)*;

h_deletion = Optimize[
    CDRewrite[u.Delete["h"] <1>, "", "", sigma_star, 'ltr', 'opt']
];

# A final syllable ending in a vowel, letter m, or diphthong is removed
# before a word beginning with a vowel (or an h-).
elision = Optimize[
    CDRewrite[u.Delete[(i.PHONEMIC_VOWEL | "oj" | "aj" | "aw") " "] <50>,
                        "", i.PHONEMIC_VOWEL, sigma_star, 'ltr', 'opt']
];

# Word-final consonant reattaches to the following word with an initial vowel.
# TODO: orthographically annotate using "‿".
resyllabification = Optimize[
    CDRewrite[u.Insert[" "] <5>,
              "", i.CONSONANT " " i.PHONEMIC_VOWEL, sigma_star, 'ltr', 'opt'] @
    CDRewrite[u.Delete[" "] <5>,
              " " i.CONSONANT, i.PHONEMIC_VOWEL, sigma_star]
];

synizesis = Optimize[
    # The consonantization of i and u into their corresponding glides, [j] and [w].
    CDRewrite[(("i" : "j") | ("u" : "w")) <500>,
                "", i.PHONEMIC_VOWEL, sigma_star, 'ltr', 'opt'] @
    # A hiatus becomes a diphthong.
    CDRewrite[(("i" : "j") | ("u" : "w")) <500>,
                i.PHONEMIC_VOWEL, "", sigma_star, 'ltr', 'opt']
];

# The vowels [i] and [u] are pronounced as separate syllables, instead of
# the normal glides, [j] and [w].
# TODO: include [j] glide?
diaeresis = Optimize[
    CDRewrite[("w" : "u") <500>, "k" | "g", "", sigma_star, 'ltr', 'opt']
];

# The lengthening of a usually short syllable.
diastole = Optimize[
    CDRewrite[(("a" : "aː") | ("e" : "eː") | ("i" : "iː") |
              ("o" : "oː") | ("u" : "uː")) <1000>,
              "", "", sigma_star, 'ltr', 'opt']
];

export OPTIONAL = Optimize[h_deletion @ resyllabification @ elision @
                           synizesis @ diaeresis @ diastole];

# TODOs:
# * Hypermeter: when a period has an extra syllable ending in a vowel
#   or m which is elided into the first word of the following period, often with -que words.
#   (1.448 ajrea kuj gradibus surgeːbant liːmina neksajkwe)
# * Systole
