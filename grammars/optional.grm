# Implements sandhi phenomena, such as elision, resyllabification,
# diaeresis, synizesis, hypermetric lengthening, etc.

import 'byte.grm' as b;
import 'inventory.grm' as i;
import 'utility.grm' as u;

sigma_star = (i.PROSODIC_SYMBOL | i.PHONEME)*;

export H_DELETION = Optimize[
    CDRewrite[u.Delete["h"] <1>, "", "", sigma_star, 'ltr', 'opt']
];

# A final syllable ending in a vowel, letter m, or diphthong is removed
# before a word beginning with a vowel (or an h-).
export ELISION = Optimize[
    CDRewrite[u.Delete[(i.PHONEMIC_VOWEL | "oj" | "aj" "aw") " " "h"?] <100>,
                        "", "h"? i.PHONEMIC_VOWEL, sigma_star, 'ltr', 'opt']
];

# Word-final consonant reattaches to the following word with an initial vowel.
# TODO: orthographically annotate using "‿".
export RESYLLABIFY = Optimize[
    CDRewrite[u.Insert[" "] <10>, "", i.CONSONANT " " i.PHONEMIC_VOWEL, sigma_star, 'ltr', 'opt'] @
    CDRewrite[u.Delete[" "] <10>, " " i.CONSONANT, i.PHONEMIC_VOWEL, sigma_star]
];

export SYNIZESIS = Optimize[
    # The consonantization of i and u into their corresponding glides, [j] and [w].
    CDRewrite[(("i" : "j") |
              ("u" : "w")) <500>, "", i.SHORT_VOWEL, sigma_star, 'ltr', 'opt'] @
    # A hiatus becomes a diphthong.
    CDRewrite[(("i" : "j") |
              ("u" : "w")) <500>, i.SHORT_VOWEL, "", sigma_star, 'ltr', 'opt']
];

# The vowels [i] and [u] are pronounced as separate syllables, instead of
# the normal glides, [j] and [w].
# TODO: include [j] glide?
export DIAERESIS = Optimize[
    CDRewrite[("w" : "u") <500>, "k" | "g", "", sigma_star, 'ltr', 'opt']
];

# The lengthening of a usually short syllable.
export DIASTOLE = Optimize[
    CDRewrite[(("a" : "aː") |
              ("e" : "eː") |
              ("i" : "iː") |
              ("o" : "oː") |
              ("u" : "uː")) <1000>, "", "", sigma_star, 'ltr', 'opt']
];

export OPTIONAL = Optimize[H_DELETION @ ELISION @ RESYLLABIFY @ SYNIZESIS @ DIAERESIS];

# TODOs:
# * Hypermeter: when a period has an extra syllable ending in a vowel
#   or m which is elided into the first word of the following period, often with -que words.
#   (1.448 ajrea kuj gradibus surgeːbant liːmina neksajkwe)
# * Diastole: a usually short syllable is long.
#   The second syllables of mihi, tibi, sibi, ubi, and ibi are often lengthened in poetry.
#   He also lengthens other syllables not usually lengthened
# * Systole
