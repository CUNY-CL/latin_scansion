# Converts normalized Latin lines to its broad phonetic transcription.

import 'byte.grm' as b;

# Note: the Romans never used "v" or "j," but some secondary sources do.
graphemes = 
    "a" | "b" | "c" | "d" | "e" | "f" | "g" |
    "h" | "i" | "j" | "k" | "l" | "m" | "n" |
    "o" | "p" | "q" | "r" | "s" | "t" | "u" |
    "v" | "w" | "x" | "y" | "z" | "ā" | "ē" |
    "ī" | "ō" | "ū" | b.kSpace;

phonemes = 
    "a" | "aː" | "ãː" | "b" | "d" | "e" | "eː" | 
    "ẽː" | "f" | "g" | "h" | "i" | "iː" | "ĩː" |
    "j" | "k" | "kw" | "l" | "m" | "n" | "ŋ" |
    "o" | "oː" | "õː" | "p" | "r" | "s" | "t" |
    "u" | "uː" | "ũː" | "w" | "z" | b.kSpace;

sigma_star = (graphemes | phonemes)*;

bow = (b.kSpace | "[BOS]"); # Beginning of a word.
eow = (b.kSpace | "[EOS]"); # End of a word.

# These words form an "eu" dipthong, rather than the normal hiatus.                                
eu_exceptions = Optimize[CDRewrite[("heus": "hews") |
                                  ("ceu" : "kew") |
                                  ("neu" : "new") |
                                  ("seu" : "sew") |
                                  ("ēheu" : "eːhew"), bow, eow, sigma_star]];

# These words form an "ou" dipthong, rather than the normal hiatus.
ou_exceptions = Optimize[CDRewrite[("boum": "bowũː") |
                                  ("prout" : "prowt"), bow, eow, sigma_star]];

# These words form an "ui" dipthong, rather than the normal hiatus. 
ui_exceptions = Optimize[CDRewrite[("hui": "huj") |
                                  ("huic" : "hujk") |
                                  ("cui" : "kuj"), bow, eow, sigma_star]];

# These words form an "ei" dipthong, rather than the normal hiatus. 
ei_exceptions = Optimize[CDRewrite[("dein": "dẽːj") |
                                  ("deinde" : "dẽːjde") |
                                  ("deinceps" : "dẽːjkeps") |
                                  ("ei" : "ej") |
                                  ("hei" : "hej"), bow, eow, sigma_star]];

# As a result of the change from Old Latin "quo" and "nguo" to
# "guu" and "nguu," loss of the preceding "u" occured.
u_loss = Optimize[CDRewrite[("nguu": "ngu") |
                            ("quu" : "cu"), "", "", sigma_star]];

qu_rule = Optimize[CDRewrite["qu" : "kw", "", "", sigma_star]];

# When an "i" occurs intervocalically (between two vowels), 
# it is realized as a glide: [jj].
vowels_except_i = "a" | "e" | "o" | "u" | "ā" | "ē" | "ō" | "ū";
intervocalic_i = Optimize[CDRewrite["i" : "jj", vowels_except_i, 
                          vowels_except_i, sigma_star]];

# The Romans wrote various Greek letters as digraphs: "ph," "th," and "ch."
# Most likely, they were pronounced as [p, t, k], respectively.
digraphs = Optimize[CDRewrite[("ph" : "p") |
                              ("ch" : "k") |
                              ("th" : "t"), "", "", sigma_star]];

# "bs" was pronounced as [ps] and "bt" was pronounced as [pt].
b_rule = Optimize[CDRewrite["b" : "p", "", "s" | "t", sigma_star]];

# Word-final "m" and "n" indicates vowel lengthening and nasalization.
nasalized_m_n = Optimize[CDRewrite[("a" : "ãː") |
                        ("e" : "ẽː") |
                        ("i" : "ĩː") |
                        ("o" : "õː") |
                        ("u" : "ũː" ), "", ("m" | "n") eow, sigma_star]];
delete_m_n = Optimize[CDRewrite[("m" | "n") : "",
                        "", 
                        eow, sigma_star]];

# If "gu" proceeds "n" and precedes a vowel, it is pronounced with a glide.
vowels = "a" | "e" | "i" | "o" | "u" | "ā" | "ē" | "ī" | "ō" | "ū";
gu_glide = Optimize[CDRewrite["gu" : "gw", "n", vowels, sigma_star]];

ng_gn_rules = Optimize[CDRewrite[("ng" : "ŋ") |
                                 ("gn" : "ŋn"), "", "", sigma_star]];

diphthongs = Optimize[CDRewrite[("ae" : "aj") | 
                                ("oe" : "oj") |
                                ("au" : "aw"), "", "", sigma_star]];

rule1 = Optimize[CDRewrite[("c" : "k") |
                           ("x" : "ks") |
                           ("v" : "w") |
                           ("y" : "u"), "", "", sigma_star]];

macrons = Optimize[CDRewrite[("ā" : "aː") |
                      ("ē" : "eː") |
                      ("ī" : "iː") |
                      ("ō" : "oː") |
                      ("ū" : "uː") |
                      ("ȳ" : "uː"), "", "", sigma_star]];

rules = Optimize[eu_exceptions @ ou_exceptions @ ui_exceptions
                @ ei_exceptions @u_loss @ qu_rule @ intervocalic_i
                @ digraphs @ b_rule @ nasalized_m_n @ delete_m_n 
                @ gu_glide @ ng_gn_rules @ diphthongs @ rule1 @ macrons];

# Limits input to the graphemes and output to the phonemes.
export PRON = graphemes* @ rules @ phonemes*;

# Tests.
test_pron_1 = AssertEqual[
    "gentis honōs haerent īnfixī pectore vultūs" @ PRON, 
    "gentis honoːs hajrent iːnfiksiː pektore wultuːs"
]; # Testing for macronized vowels, "ae" diphthong, "v," "x," and "c."

test_pron_2 = AssertEqual[
    "heu quianam tantī cīnxērunt aethera nimbī" @ PRON, 
    "heu kwianãː tantiː kiːnkseːrunt ajtera nimbiː"
]; # Testing for intervocalic "i," "qu," "th," and nasalized "m."

test_pron_3 = AssertEqual[
    "nec tēcum ausonium quīcumque est quaerere thybrim" @ PRON, 
    "nek teːkũː awsoniũː kwiːkumkwe est kwajrere tubrĩː"
]; # Testing for "y."

test_pron_4 = AssertEqual[
    "dīxerat haec adytīs cum lūbricus anguis ab īmīs" @ PRON, 
    "diːkserat hajk adutiːs kũː luːbrikus aŋwis ab iːmiːs"
]; # Testing for "ng."

test_pron_5 = AssertEqual[
    "centaurō invehitur magnā scyllāque cloanthus" @ PRON, 
    "kentawroː inwehitur maŋnaː skullaːkwe kloantus"
]; # Testing for "gn."

test_pron_6 = AssertEqual[
    "subsīdunt undae tumidumque sub axe tonantī" @ PRON, 
    "supsiːdunt undaj tumidumkwe sub akse tonantiː"
]; # Testing for "bs."

test_pron_7 = AssertEqual[
    "vēlōcem mnēstheus agit ācrī rēmige pristim" @ PRON, 
    "weːloːkẽː mneːsteus agit aːkriː reːmige pristĩː"
]; # Testing to make sure "mnēstheus" does not glide.

test_pron_8 = AssertEqual[
    "ac prior heus inquit juvenēs mōnstrāte meārum" @ PRON, 
    "ak prior hews inkwit juweneːs moːnstraːte meaːrũː"
]; # Testing to make sure "heus" glides.

test_pron_9 = AssertEqual[
    "expediunt corpusque lavant frīgentis et unguunt" @ PRON, 
    "ekspediunt korpuskwe lawant friːgentis et uŋunt"
]; # Testing for "nguu."

test_pron_10 = AssertEqual[
    "prīmus equum phalerīs īnsignem victor habētō" @ PRON, 
    "priːmus ekũː paleriːs iːnsiŋnẽː wiktor habeːtoː"
]; # Testing for "quu."

# TODO:
# • Uncertainty for quīcumque: probably either kwiːkuŋkwe or kwiːkũːkwe.
# • Same for tumidumque.
# • The clitics -que, -ne and -ue should designate eow.
# • When final m was followed by a plosive or nasal consonant, 
#   it was assimilated to the following consonant.